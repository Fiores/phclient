import com.android.build.gradle.internal.dsl.BuildType as AndroidBuildType
import com.android.builder.internal.ClassFieldImpl

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

Properties local = new Properties()
local.load(new FileReader("local.properties"))

static void bc(AndroidBuildType self, String name, String value) {
    (self as AndroidBuildType).buildConfigFields[name] = new ClassFieldImpl("String", name, "\"$value\"")
}
static void bc(AndroidBuildType self, String name, Boolean value) {
    (self as AndroidBuildType).buildConfigFields[name] = new ClassFieldImpl("Boolean", name, value.toString())
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "ru.ponyhawks.android"
        //noinspection MinSdkTooLow
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 11
        versionName "1.22.3"
    }

    signingConfigs {
        release {
            storeFile file(local.get("key.keystore.path"))
            storePassword local.get("key.keystore.pwd")
            keyAlias local.get("key.alias")
            keyPassword local.get("key.alias.pwd")
        }
    }

    buildTypes {
        defaultConf {
            bc delegate, "IMGUR_CLIENT_ID", local.get("imgur.client_id")
        }

        debug {
            initWith defaultConf
            signingConfig signingConfigs.release
        }

        release {
            initWith defaultConf

            minifyEnabled true
            shrinkResources true
            useProguard false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

String support_v = '27.+'
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'], exclude: ['*-sources.jar'])

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'

    implementation "com.android.support:appcompat-v7:$support_v"
    implementation "com.android.support:support-core-ui:$support_v"
    implementation "com.android.support:design:$support_v"

    implementation 'com.cab404:chumroll:1.1.5'

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
}
